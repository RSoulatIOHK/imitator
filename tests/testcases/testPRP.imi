(************************************************************
 *                      IMITATOR MODEL                      
 *
 * Empty template of a case study
 *
 * Description     : Sample PTA to test PRP
 * Correctness     : locBad1 and locBad2 unreachable
 * Source          : Own work
 * Author          : Étienne André
 * Modeling        : Étienne André
 * Input by        : Étienne André
 * License         : Creative Commons Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)

 *
 * Created         : 2016/01/15
 * Last modified   : 2020/04/10
 *
 * IMITATOR version: 3
 ************************************************************)

var

(* Clocks *)
 	x, y
		: clock;

(* Parameters *)
	p1, p2
		: parameter;



(************************************************************)
  automaton pta
(************************************************************)
(* synclabs: a; *)

loc l1: invariant True 
	when True goto l2;
	(* p2 >= 4 *)
	when x >= 4 & y = p2 do {x := 0, y := 0} goto locBad1;
	(* p2 >= 5 *)
	when x >= 5 & y = p2 do {x := 0, y := 0} goto infiniteLoop;

loc l2: invariant True 
	(* p2 <= 1 *)
	when x <= 1 & y = p2 do {x := 0, y := 0} goto l3;
	(* p1 >= p2 *)
	when p1 >= x & y >= p2 do {x := 0, y := 0} goto l4;
	(* p1 >= 3 *)
	when x >= 3 & y = p1 do {x := 0, y := 0} goto locBad2;

(* Good infinite loop location *)
loc l3: invariant x <= p1 
	when x = p1 do {x := 0, y := 0} goto l3;

(* Good deadlock location *)
loc l4: invariant True 

loc locBad1: invariant True 

loc locBad2: invariant True 
	(* Go to infinite loop, but PRP shall not go there *)
	when True do {x := 0, y := 0} goto infiniteLoop;

(* Simulate an infinite loop for IMITATOR (with some extrapolation, this loop could terminate, but then we could use another PTA gadget to simulate an infinite loop) *)
loc infiniteLoop: invariant x <= 1 
	when x = 1 do {x := 0} goto infiniteLoop;

end (* pta *)



(************************************************************)
(* Initial state *)
(************************************************************)

init :=
	(*------------------------------------------------------------*)
	(* Initial location *)
	(*------------------------------------------------------------*)
	& loc[pta] = l1

	(*------------------------------------------------------------*)
	(* Initial clock constraints *)
	(*------------------------------------------------------------*)
	& x = 0
	& y = 0

	(*------------------------------------------------------------*)
	(* Parameter constraints *)
	(*------------------------------------------------------------*)
	& p1 >= 0
	& p2 >= 0
;



(************************************************************)
(* The end *)
(************************************************************)
end
